Shader "Custom/BlinkingTutorialPreviewBlock"
{
    Properties
    {
        // テクスチャ
        _MainTex ("Texture", 2D) = "white" {}               // 設定するテクスチャ
        _TextureDensity ("Texture Density", float) = 0.35    // テクスチャの色
        // 外枠
        _EdgeColor ("Edge Color", Color) = (0,0,0,1)    // 色
        _EdgeShine ("Edge Shine",Range(0,50)) = 35      // 明るさ
        // 面
        _SurfaceColor ("Surface Color", Color) = (1,1,1,1)       // 色       
        _SurfaceDarkness ("Surface Darkness",Range(0,1)) = 0.8  // 暗さ
        // 透明
        _TransparencyInterval ("Transparency Interval",Range(0.1,10)) = 1.5 // 透明化間隔時間
        _MaximumOpacity ("Maximum Opacity",Range(0,1)) = 0.95               // 最大不透明
        _MinimalOpacity ("Minimal Opacity",Range(0,1)) = 0.1                // 最小不透明
    }
    SubShader
    {
        Tags { "RenderType"="Transparent" "Queue" = "Transparent"} 
        Blend SrcAlpha OneMinusSrcAlpha
        Cull Off
        LOD 100 

        Pass
        {
            CGPROGRAM 

            #pragma vertex vert 
            #pragma fragment frag 
            #pragma multi_compile_fog 

            #include "UnityCG.cginc" 

            // マクロを定義
            #define HALF_SYCLE 0.5  // 周期の半分
            #define SCALE_FACTOR 2  // 0〜1に修正 
           
            struct appdata
            {
                float4 vertex : POSITION; 
                float2 uv : TEXCOORD0; 
                fixed4	color : COLOR;
            };

            struct v2f
            {
                float2 uv : TEXCOORD0; 
                UNITY_FOG_COORDS(1) 
                float4 vertex : SV_POSITION; 
            };

            sampler2D _MainTex; 
            float4 _MainTex_ST;            
            float4 _EdgeColor;
            float _EdgeShine;
            float4 _SurfaceColor;
            float _SurfaceDarkness;
            float _Transparency;
            float _TextureDensity;

            float _TransparencyInterval;
            float _MaximumOpacity;
            float _MinimalOpacity;

            v2f vert (appdata v)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex); 
                o.uv = TRANSFORM_TEX(v.uv, _MainTex); 

                UNITY_TRANSFER_FOG(o,o.vertex); 
                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                fixed4 col = tex2D(_MainTex, i.uv); 

                // テクスチャの明るさを計算
                float brightness = dot(col.rgb, float3(0.299, 0.587, 0.114));

                // 暗い部分をエッジと判定
                if(brightness < _TextureDensity)
                {                      
                    // 色を設定
                    col.rgb = _EdgeColor.rgb;
                    // 明るさを設定
                    col.rgb *= _EdgeShine;
                }
                // 明るい部分を面と判定
                else
                {
                    // 色を設定
                    col.rgb = _SurfaceColor.rgb;   
                    // 暗さを設定
                    col.rgb *= _SurfaceDarkness;
                }

                // 透明化間隔時間の正規化
                float transparencyTime = fmod(_Time.y, _TransparencyInterval) / _TransparencyInterval;
                // 1〜0の値を往復する
                float interpolatedValue = (transparencyTime < HALF_SYCLE) ? transparencyTime * SCALE_FACTOR : 1 - (transparencyTime - HALF_SYCLE) * SCALE_FACTOR;
                // 滑らかに透明度を変更する
                col.a = lerp(_MaximumOpacity, _MinimalOpacity, interpolatedValue);

                return col;
            }
            ENDCG 
        }
    }
}